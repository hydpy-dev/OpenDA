plugins {
    id 'java-library'
}

project.version = '0.6.4'
project.description = 'The HydPy BlackBoxModel wrapper for OpenDA'

// OpenDA is currrently distributed with Java 1.7
def javaVersion = JavaVersion.VERSION_1_7;
sourceCompatibility = javaVersion;
targetCompatibility = javaVersion; // defaults to sourceCompatibility

repositories {
    jcenter()
    
    flatDir {
        dirs "$projectDir/externalLibs/OpenDA"
    }
}

dependencies {
    implementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.6'

    // Those libs are part of OpenDA and will be automatically in the classpath when we run OpenDA with this extension.
    // So they will be not included in our distribution. 
    compileOnly name: 'joda-time-2.9.6'
    compileOnly name: 'commons-lang3-3.4'
    compileOnly name: 'openda_core'

    // Use JUnit test framework
    // testImplementation 'junit:junit:4.12'
}

jar {
	// name the main jar file according to OpenDA convention
	baseName  = 'model_hydpy'

    manifest {
        attributes(
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            'Build-Version'  : "${project.version}",
            'Created-By'     : "Gradle ${gradle.gradleVersion}",
            'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
}

task printVersionName {
    doLast {
        println project.version
    }
}

// Distribute compilation and non-OpenDA libs into one single zip file
task buildZip(type: Zip) {
    from jar
    from configurations.runtimeClasspath
}

build.dependsOn buildZip